module

meta import CustomPrelude
public import Core.TypedTLAPlus.Coercion
public import Core.ComputableTLAPlus.Syntax
public import Core.ComputableTLAPlus.FreeVars

public section


/-!
  `Coercion.applyComputable` is the second of `Core/TypedTLAPlus/Coercion.lean`'s two structural
  recursions over `TypedTLAPlus.Coercion`, discharging against `ComputableTLAPlus.Expression`
  instead of `TypedTLAPlus.Expression`. Needed because a `receive`'s channel/reference coercion is
  stored unapplied and survives past `Typed2Computable`'s type change: `Guarded2Network` is the
  first pass with a concrete `ComputableTLAPlus.Expression` (the built `Head(inbox)`/`Tail(inbox)`
  expression) to discharge it against, so it can't reuse `Coercion.apply` (fixed at
  `TypedTLAPlus.Expr`).

  Mirrors `Coercion.apply` case-for-case, except `choose`'s domain here is a required `Expression
  α` rather than `Option (Expression α)` — see `Core/ComputableTLAPlus/Syntax.lean`'s module doc.
-/

namespace TypedTLAPlus

/-- Checked TLA⁺ expressions at `ComputableTLAPlus`'s output type — what `Coercion.applyComputable`
transforms. -/
abbrev CExpr := ComputableTLAPlus.Expression ComputableTLAPlus.Typ

/-- Applies a coercion to an already-built `ComputableTLAPlus.Expression` — see the module doc
above for why this can't reuse `Coercion.apply`. Registers every synthesized node at the coerced
expression's own span, for the reason spelled out on `Coercion.apply`. -/
@[expose] def Coercion.applyComputable (c : Coercion) (e : CExpr) : CExpr :=
  let pos := posOf e
  match c with
  | .id => e
  | .strToSeq =>
    .opCall (.var (.operator [.str] (.seq .int)) (.intrinsic "StrToSeq") @@ pos) [e] @@ pos
  | .seqToFun τ₀ i =>
    let range : CExpr :=
      .opCall (.var (.operator [.int, .int] (.set .int)) (.module "Naturals" "..") @@ pos)
        [.nat (toString (1 : Nat)) @@ pos,
         .opCall (.var (.operator [.seq τ₀] .int) (.module "Sequences" "Len") @@ pos) [e] @@ pos] @@ pos
    .fn i .int τ₀ range
      (.fnCall (ComputableTLAPlus.Expression.liftBound 1 e) (.seq τ₀) (.var .int (.bound 0) @@ pos) @@ pos) @@ pos
  | .bagToFun τ₀ i =>
    let domain : CExpr :=
      .opCall (.var (.operator [.bag τ₀] (.set τ₀)) (.module "Bags" "BagToSet") @@ pos) [e] @@ pos
    .fn i τ₀ .int domain
      (.opCall (.var (.operator [τ₀, .bag τ₀] .int) (.module "Bags" "CopiesIn") @@ pos)
        [.var τ₀ (.bound 0) @@ pos, ComputableTLAPlus.Expression.liftBound 1 e] @@ pos) @@ pos
  | .tupleToSeq n τ _ =>
    .seq ((List.range n).map λ i ↦
      .fnCall e (.tuple (List.replicate n τ)) (.nat (toString (i + 1)) @@ pos) @@ pos) τ @@ pos
  | .set x τ τ' c =>
    .map' (c.applyComputable (.var τ (.bound 0) @@ pos)) x τ τ' e @@ pos
  | .tuple coes τs τs' =>
    (.tuple <| (List.range coes.length).attach.map λ ⟨i, hi⟩ ↦
      (τs'[i]!, (coes[i]'(List.mem_range.mp hi)).applyComputable
        (.fnCall e (.tuple τs) (.nat (toString (i + 1)) @@ pos) @@ pos))) @@ pos
  | .record fields =>
    (.record <| fields.attach.map λ ⟨⟨name, c, τ'ᵢ⟩, _hf⟩ ↦
      (τ'ᵢ, name, c.applyComputable (.recordAccess e name @@ pos))) @@ pos
  | .function x y dom rng dom' rng' cDom cRng =>
    let eLift : CExpr := ComputableTLAPlus.Expression.liftBound 1 e
    let domainExpr : CExpr :=
      .opCall (.var (.operator [.function dom rng] (.set dom)) (.intrinsic "DOMAIN") @@ pos) [e] @@ pos
    let newDomain : CExpr :=
      .map' (cDom.applyComputable (.var dom (.bound 0) @@ pos)) x dom dom' domainExpr @@ pos
    let eqTy : Typ := .operator [dom', dom'] .bool
    let domainExprLift : CExpr :=
      .opCall (.var (.operator [.function dom rng] (.set dom)) (.intrinsic "DOMAIN") @@ pos) [eLift] @@ pos
    let recoveredArg : CExpr :=
      .choose x dom domainExprLift
        (.opCall (.var eqTy (.intrinsic "=") @@ pos)
          [cDom.applyComputable (.var dom (.bound 0) @@ pos), .var dom' (.bound 1) @@ pos] @@ pos) @@ pos
    .fn y dom' rng' newDomain
      (cRng.applyComputable (.fnCall eLift (.function dom rng) recoveredArg @@ pos)) @@ pos
  | .comp c₁ c₂ => c₂.applyComputable (c₁.applyComputable e)
  termination_by sizeOf c
  decreasing_by
    1,4-7: decreasing_trivial
    1:
      have h := List.sizeOf_lt_of_mem (List.getElem_mem (List.mem_range.mp ‹_›))
      apply lt_trans h
      decreasing_trivial
    · have h := List.sizeOf_lt_of_mem ‹_ ∈ _›
      apply lt_trans ?_ (lt_trans h ?_) <;> decreasing_trivial

end TypedTLAPlus

namespace ComputableTLAPlus

/-! ## `Coercion.applyComputable` and free variables

`freeVars_applyComputable_subset` — a coercion adds no free variable — lives here rather than next
to the semantics: it needs `Coercion.applyComputable`'s induction principle, generated only in this
module. The small `mem_freeVars_*` unfoldings it uses are re-exported for the semantics to reuse.

TODO(locally-nameless): re-derive `freeVars_applyComputable_subset` against the de Bruijn
`applyComputable`/`liftBound`; parked with the semantics port. -/

variable {z : String}

@[simp] theorem freeVars_var_free {n : String} {τ : Typ} :
    (Expression.var τ (.free n)).freeVars = {n} := by simp only [Expression.freeVars]

@[simp] theorem freeVars_var_bound {i : Nat} {τ : Typ} :
    (Expression.var τ (.bound i)).freeVars = ∅ := by simp only [Expression.freeVars]

@[simp] theorem freeVars_var_intrinsic {n : String} {τ : Typ} :
    (Expression.var τ (.intrinsic n)).freeVars = ∅ := by simp only [Expression.freeVars]

@[simp] theorem freeVars_var_module {m n : String} {τ : Typ} :
    (Expression.var τ (.module m n)).freeVars = ∅ := by simp only [Expression.freeVars]

@[simp] theorem freeVars_nat {s : String} : (Expression.nat s : Expression Typ).freeVars = ∅ := by
  simp only [Expression.freeVars]

@[simp] theorem freeVars_str {s : String} : (Expression.str s : Expression Typ).freeVars = ∅ := by
  simp only [Expression.freeVars]

theorem mem_freeVars_fn {y : String} {a co : Typ} {dom body : Expression Typ} :
    z ∈ (Expression.fn y a co dom body).freeVars ↔ z ∈ dom.freeVars ∨ z ∈ body.freeVars := by
  rw [Expression.freeVars, Finset.mem_union]

theorem mem_freeVars_map' {y : String} {a co : Typ} {dom body : Expression Typ} :
    z ∈ (Expression.map' body y a co dom).freeVars ↔ z ∈ dom.freeVars ∨ z ∈ body.freeVars := by
  rw [Expression.freeVars, Finset.mem_union]

theorem mem_freeVars_choose {y : String} {a : Typ} {dom body : Expression Typ} :
    z ∈ (Expression.choose y a dom body).freeVars ↔ z ∈ dom.freeVars ∨ z ∈ body.freeVars := by
  rw [Expression.freeVars, Finset.mem_union]

theorem mem_freeVars_fnCall {f e' : Expression Typ} {a : Typ} :
    z ∈ (Expression.fnCall f a e').freeVars ↔ z ∈ f.freeVars ∨ z ∈ e'.freeVars := by
  rw [Expression.freeVars, Finset.mem_union]

/-- A `.fnCall` at a `.nat` index reads only its head. -/
theorem freeVars_fnCall_nat {e : Expression Typ} {τ : Typ} {s : String} :
    (Expression.fnCall e τ (.nat s)).freeVars = e.freeVars := by
  rw [Expression.freeVars]; simp

/-- Field access reads only its subject. -/
theorem freeVars_recordAccess {e : Expression Typ} {n : String} :
    (Expression.recordAccess e n).freeVars = e.freeVars := by simp only [Expression.freeVars]

/-- A coercion adds no free variable: every binder `applyComputable` introduces refers to itself
by a `.bound` index, `liftBound` never touches a `.free` name, and every splice of `e` sits under
an operator (`Len`/`DOMAIN`/`.fnCall`/`.recordAccess`) that carries `e`'s free variables through
unchanged. `ExprSemantics.evalCoerce` needs this to shrink a `Coercion.FreshFor` hypothesis onto a
sub-expression (`.comp`, `.function`). -/
theorem freeVars_applyComputable_subset {c : TypedTLAPlus.Coercion} {e : Expression Typ}
    (hz : z ∈ (TypedTLAPlus.Coercion.applyComputable c e).freeVars) : z ∈ e.freeVars := by
  revert hz
  fun_induction TypedTLAPlus.Coercion.applyComputable c e with
  | case1 => exact id
  | case2 => next e' pos =>
    simp only [Expression.mem_freeVars_opCall, freeVars_var_intrinsic, Finset.notMem_empty,
      false_or, List.mem_singleton, exists_eq_left, imp_self]
  | case3 => next e' pos τ₀ i range =>
    intro hz
    simp only [range, mem_freeVars_fn, mem_freeVars_fnCall, Expression.mem_freeVars_opCall,
      freeVars_var_module, freeVars_var_bound, Finset.notMem_empty, false_or, or_false,
      List.mem_cons, List.not_mem_nil] at hz
    grind [Expression.freeVars_liftBound_subset, freeVars_nat, Expression.mem_freeVars_opCall,
      freeVars_var_module]
  | case4 => next e' pos τ₀ i domain =>
    intro hz
    simp only [domain, mem_freeVars_fn, Expression.mem_freeVars_opCall, freeVars_var_module,
      Finset.notMem_empty, false_or, exists_eq_left, List.mem_cons, List.not_mem_nil,
      or_false] at hz
    grind [Expression.freeVars_liftBound_subset, freeVars_var_bound]
  | case5 => next e' pos n τ hn =>
    intro hz
    simp only [Expression.mem_freeVars_seq, List.mem_map] at hz
    grind [freeVars_fnCall_nat]
  | case6 => next e' pos x τ τ' cc ih =>
    intro hz
    rw [mem_freeVars_map'] at hz
    rcases hz with h | h
    · exact h
    · absurd ih h
      simp
  | case7 => next e' pos coes τs τs' ih1 =>
    intro hz
    rw [Expression.mem_freeVars_tuple] at hz
    obtain ⟨p, hp, h⟩ := hz
    rw [List.mem_map] at hp
    obtain ⟨⟨j, hj⟩, -, rfl⟩ := hp
    simp only at h
    have hf := ih1 j hj h
    rwa [freeVars_fnCall_nat] at hf
  | case8 => next e' pos fields ih1 =>
    intro hz
    rw [Expression.mem_freeVars_record] at hz
    obtain ⟨p, hp, h⟩ := hz
    rw [List.mem_map] at hp
    obtain ⟨⟨⟨name, cc, τ'ᵢ⟩, hf⟩, -, rfl⟩ := hp
    simp only at h
    have hr := ih1 name cc τ'ᵢ hf h
    rwa [freeVars_recordAccess] at hr
  | case9 => next e' pos x y dom rng dom' rng' cDom cRng eLift domE newD eqTy domEL rArg ih3 ih2 ih1 =>
    intro hz
    rw [mem_freeVars_fn] at hz
    rcases hz with h | h
    · simp only [newD, domE, mem_freeVars_map', Expression.mem_freeVars_opCall,
        freeVars_var_intrinsic, Finset.notMem_empty, false_or, List.mem_singleton,
        exists_eq_left] at h
      rcases h with h | h
      · exact h
      · absurd ih3 h
        simp
    · have hb := ih1 h
      rw [mem_freeVars_fnCall] at hb
      rcases hb with h | h
      · exact Expression.freeVars_liftBound_subset h
      · simp only [rArg, domEL, mem_freeVars_choose, Expression.mem_freeVars_opCall,
          freeVars_var_intrinsic, Finset.notMem_empty, false_or, exists_eq_left, List.mem_cons,
          List.not_mem_nil, or_false] at h
        rcases h with h | ⟨a, rfl | rfl, ha⟩
        · exact Expression.freeVars_liftBound_subset h
        · absurd ih2 ha
          simp
        · absurd ha
          simp
  | case10 => next e' c₁ c₂ ih₁ ih₂ => exact λ hz ↦ ih₁ (ih₂ hz)

/-- The `⊆` reading of `freeVars_applyComputable_subset`. -/
theorem freeVars_applyComputable_subset' {c : TypedTLAPlus.Coercion} {e : Expression Typ} :
    (TypedTLAPlus.Coercion.applyComputable c e).freeVars ⊆ e.freeVars :=
  λ _ hz ↦ freeVars_applyComputable_subset hz

/-! ## `Coercion.applyComputable` and `openVar`

`applyComputable` commutes with an `openVar` traversal: every binder it introduces refers to itself
by `.bound`, every splice of `e` under a binder carries a `liftBound 1` that `openVar` at the
raised depth cancels (`Expression.openVar_liftBound_one_comm`), and every other node passes the
traversal through. `ExprSemantics.evalCoerce`'s `.set`/`.function` cases need this: the built `.fn`/
`.map'` body opens with the binder's hint, and the opened term must read as `applyComputable`
of the opened argument so the coercion recursion applies. -/

set_option maxHeartbeats 1000000 in
/-- `mapVars (openVarLam x) k` pushes through `applyComputable`: the depth-generalised statement,
proved by recursion on the coercion. `openVar_applyComputable` is the `k = 0` reading. -/
theorem openVar_applyComputable_aux (c : TypedTLAPlus.Coercion) (x : String) :
    ∀ (e : Expression Typ) (k : Nat),
      Expression.mapVars (Expression.openVarLam x) k (c.applyComputable e)
        = c.applyComputable (Expression.mapVars (Expression.openVarLam x) k e) := by
  intro e
  fun_induction TypedTLAPlus.Coercion.applyComputable c e with
  | case1 => simp_intro k [TypedTLAPlus.Coercion.applyComputable]
  | case2 => next e' pos =>
    simp_intro k [TypedTLAPlus.Coercion.applyComputable, Expression.mapVars, Expression.openVarLam,
      registerSource, List.attach_map_val, List.map_cons, List.map_nil]
  | case3 => next e' pos τ₀ i range =>
    intro k
    simp only [TypedTLAPlus.Coercion.applyComputable, Expression.mapVars, registerSource, range,
      Expression.openVar_liftBound_one_comm, Expression.openVarLam, List.map_cons, List.map_nil,
      List.attach_map_val]
    rw [if_neg (by omega), if_neg (by omega)]
  | case4 => next e' pos τ₀ i domain =>
    intro k
    simp only [TypedTLAPlus.Coercion.applyComputable, Expression.mapVars, registerSource, domain,
      Expression.openVar_liftBound_one_comm, Expression.openVarLam, List.map_cons, List.map_nil,
      List.attach_map_val]
    rw [if_neg (by omega), if_neg (by omega)]
  | case5 => next e' pos n τ hn =>
    simp_intro k [TypedTLAPlus.Coercion.applyComputable, Expression.mapVars, registerSource,
      List.attach_map_val, List.map_map, Function.comp_def]
  | case6 => next e' pos x' τ τ' cc ih =>
    intro k
    simp only [TypedTLAPlus.Coercion.applyComputable, Expression.mapVars, registerSource] at ih ⊢
    congr 1
    rewrite [ih (k + 1)]
    simp only [Expression.openVarLam, registerSource]
    rw [if_neg (by omega), if_neg (by omega)]
  | case7 => next e' pos coes τs τs' ih1 =>
    intro k
    simp only [TypedTLAPlus.Coercion.applyComputable, Expression.mapVars, registerSource] at ih1 ⊢
    congr 1
    refine Expression.doubleAttach_map_eq λ ip _ ↦ ?_
    obtain ⟨i, hi⟩ := ip
    refine Prod.ext rfl ?_
    dsimp only
    rw [ih1 i hi k]
  | case8 => next e' pos fields ih1 =>
    intro k
    simp only [TypedTLAPlus.Coercion.applyComputable, Expression.mapVars, registerSource] at ih1 ⊢
    congr 1
    refine Expression.doubleAttach_map_eq λ fp _ ↦ ?_
    obtain ⟨⟨nm, cc, τ'ᵢ⟩, hf⟩ := fp
    refine Prod.ext rfl (Prod.ext rfl ?_)
    dsimp only
    rw [ih1 nm cc τ'ᵢ hf k]
  | case9 =>
    next e' pos x' y dom rng dom' rng' cDom cRng eLift domE newD eqTy domEL rArg ih3 _ih2 ih1 =>
    intro k
    simp only [TypedTLAPlus.Coercion.applyComputable, Expression.mapVars, registerSource,
      eLift, domE, newD, domEL, rArg, eqTy,
      List.map_cons, List.map_nil, List.attach_map_val] at ih1 ih3 ⊢
    rewrite [ih1 (k + 1)]
    simp only [ih3, Expression.openVar_liftBound_one_comm, Expression.openVarLam, registerSource]
    rw [if_neg (by omega), if_neg (by omega), if_neg (by omega), if_neg (by omega),
      if_neg (by omega), if_neg (by omega)]
  | case10 => next e' c₁ c₂ ih₁ ih₂ =>
    intro k
    simp only [TypedTLAPlus.Coercion.applyComputable]
    rw [ih₂ k, ih₁ k]

/-- Applying a coercion and then opening the enclosing binder is opening it inside the argument
first: `openVar` slides through `applyComputable`. -/
theorem openVar_applyComputable (c : TypedTLAPlus.Coercion) (x : String) (e : Expression Typ) :
    (c.applyComputable e).openVar x = c.applyComputable (e.openVar x) :=
  openVar_applyComputable_aux c x e 0

/-- A coercion preserves local closedness. `applyComputable` introduces binders but every splice of
`e` under one carries a `liftBound 1`, so opening the enclosing binder slides through
(`openVar_applyComputable`) and lands on `e`, which a locally-closed `e` is fixed by — so the whole
term is fixed by `openVar`, hence locally closed (`Expression.LC.of_openVar_eq`). -/
theorem Expression.LC.applyComputable {c : TypedTLAPlus.Coercion} {e : Expression Typ}
    (he : e.LC) : (c.applyComputable e).LC := by
  have hx : Expression.openVar "x" e = e := he.mapVars_openVarLam_eq "x" 0
  refine Expression.LC.of_openVar_eq (name := "x") ?_
  rw [openVar_applyComputable, hx]

end ComputableTLAPlus

end
